buildscript {
    ext.version_kotlin = '1.1.2-2'
    ext.version_rxjava = '2.1.0'
    ext.version_rxkotlin = '2.0.2'
    ext.version_antlr = '4.7'
    ext.version_huctl = '3.0.0'
    ext.version_dokka = '0.9.13'

    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$version_kotlin"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$version_dokka"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'antlr'
apply plugin: 'maven'
apply plugin: 'jacoco'
apply plugin: 'org.jetbrains.dokka'

group = "com.github.sybila"
version = '3.0.0'

//antlr config
generateGrammarSource {
    arguments += [
            "-no-visitor",
            "-listener"
    ]
}

// make the Java compile task depend on the antlr4 task
compileKotlin.dependsOn generateGrammarSource

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url 'http://repo.spring.io/milestone' } // reactor
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile "org.jetbrains.kotlin:kotlin-test:$version_kotlin"

    compile "com.github.sybila:huctl:$version_huctl"
    compile "io.reactivex.rxjava2:rxjava:$version_rxjava"
    compile "io.reactivex.rxjava2:rxkotlin:$version_rxkotlin"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$version_kotlin"

    compile "io.projectreactor:reactor-core:3.1.0.M1"

    antlr "org.antlr:antlr4:$version_antlr"
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

// verbose test logging
test {
    testLogging {
        events "skipped", "failed", "standardOut", "standardError"
    }
    void
}

//travis has some strict RAM restrictions, can't afford to exceed this on the CI server
tasks.withType(Test) {
    maxHeapSize = "1024m"
    jvmArgs "-Xmx1024m"
}


dokka {
    moduleName = "Coloured Model Checking"
    sourceDirs = files('src/main/java')     // excludes generated antlr classes
    /*includes = [
            'src/main/java/com/github/sybila/package.md',
            'src/main/java/com/github/sybila/huctl/package.md',
            'src/main/java/com/github/sybila/huctl/parser/package.md',
            'src/main/java/com/github/sybila/huctl/dsl/package.md'
    ]*/
}