buildscript {
    ext.kotlin_version = '1.0.6'

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'com.madvay.tools.build.gitbuildinfo' version '0.1.5-alpha'
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'antlr'
apply plugin: 'maven'
apply plugin: 'jacoco'

group = "com.github.sybila"
version = '2.0.0'

//antlr config
generateGrammarSource {
    arguments += [
            "-no-visitor",
            "-listener"
    ]
}

// make the Java compile task depend on the antlr4 task
compileKotlin.dependsOn generateGrammarSource

// add the generated source files to the list of java sources
sourceSets.main.java.srcDirs += new File('src/main/gen')

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile 'com.intellij:annotations:5.1'
    compile 'com.github.daemontus:jafra:1.0.8'
    compile 'com.github.daemontus:egholm:0.3.0'
    compile 'com.github.sybila:ctl-parser:2.2.0'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    antlr "org.antlr:antlr4:4.6"
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    test.java.srcDirs += 'src/test/kotlin'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

jacoco {
    toolVersion = "0.7.5.201505241946"
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

test {
    testLogging {
        events "passed", "skipped", "failed"
    }
    void
}

//travis has some strict RAM restrictions, can't afford to exceed this
tasks.withType(Test) {
    maxHeapSize = "1024m"
    jvmArgs "-Xmx1024m"
}

buildStamp {
    // The git commit SHA will be appended to this url to generate the final url.
    repoBaseUrl "https://github.com/sybila/ctl-model-checker/tree/"
    // BuildInfo.java will be added under this package, in a
    // generated build/ directory.
    packageName "com.github.sybila.checker"
}